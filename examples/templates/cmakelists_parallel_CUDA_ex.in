# -----------------------------------------------------------------
# Programmer(s): Slaven Peles and Cody Balos @ LLNL
# -----------------------------------------------------------------
# SUNDIALS Copyright Start
# Copyright (c) 2002-2019, Lawrence Livermore National Security
# and Southern Methodist University.
# All rights reserved.
#
# See the top-level LICENSE and NOTICE files for details.
#
# SPDX-License-Identifier: BSD-3-Clause
# SUNDIALS Copyright End
# -----------------------------------------------------------------
# CMakeLists.txt for @SOLVER@ MPI+CUDA examples.
#
# This file is generated from a template using various variables
# set at configuration time. It can be used as a template for
# other user CMakeLists configuration files.
# -----------------------------------------------------------------

cmake_minimum_required(VERSION 3.10)

# Use the same compilers used to build SUNDIALS
set(CMAKE_C_COMPILER
  @MPI_C_COMPILER@
  CACHE FILEPATH "C compiler")
set(CMAKE_C_FLAGS
  "@CMAKE_C_FLAGS@"
  CACHE STRING "C compiler flags")

set(CMAKE_CXX_COMPILER
  @MPI_CXX_COMPILER@
  CACHE FILEPATH "C++ compiler")
set(CMAKE_CXX_FLAGS
  "@CMAKE_CXX_FLAGS@"
  CACHE STRING "C++ compiler flags")

set(CMAKE_CUDA_COMPILER
  @CMAKE_CUDA_COMPILER@
  CACHE FILEPATH "CUDA compiler")
set(CMAKE_CUDA_HOST_COMPILER
  @MPI_CXX_COMPILER@
  CACHE FILEPATH "CUDA host compiler")

# Specify project name and languages
project(@SOLVER@_mpi_cuda_examples C CXX CUDA)

# Enable testing
include(CTest)

# ------------------------------------------------------------------------------

# Specify the path to SUNDIALS header files
set(SUNDIALS_INC_DIR
  @CMAKE_INSTALL_PREFIX@/include
  CACHE PATH "Location of SUNDIALS header files")

# Specify the path to SUNDIALS libraries
set(SUNDIALS_LIB_DIR
  @CMAKE_INSTALL_PREFIX@/@CMAKE_INSTALL_LIBDIR@
  CACHE PATH "Location of SUNDIALS libraries")

# Find the @SOLVER@ library
find_library(SUNDIALS_SOLVER_LIB
  @SOLVER_LIB@ ${SUNDIALS_LIB_DIR}
  DOC "@SOLVER@ library")

if(NOT SUNDIALS_SOLVER_LIB)
  set(SUNDIALS_SOLVER_LIB "")
endif()

# Find the NVECTOR library
find_library(SUNDIALS_NVEC_LIB
  sundials_nveccuda ${SUNDIALS_LIB_DIR}
  DOC "NVECTOR CUDA library")

find_library(SUNDIALS_MPIPLUSX_LIB
  sundials_nvecmpiplusx ${SUNDIALS_LIB_DIR}
  DOC "NVECTOR mpiplusx library")

# Set additional libraries
set(SUNDIALS_EXTRA_LIB @LIBS@ CACHE STRING "Additional libraries")

# List of all libraries
set(SUNDIALS_LIBS
  ${SUNDIALS_SOLVER_LIB}
  ${SUNDIALS_NVEC_LIB}
  ${SUNDIALS_MPIPLUSX_LIB}
  ${SUNDIALS_EXTRA_LIB})

# ------------------------------------------------------------------------------

# Set the names of the examples to be built and their dependencies
set(examples @EXAMPLES@)
set(examples_dependencies @EXAMPLES_DEPENDENCIES@)
if(examples)
  list(REMOVE_DUPLICATES examples)
endif()

# Create targets for each example
foreach(example ${examples})

  # example source files
  add_executable(${example} ${example}.cu ${examples_dependencies})

  # set c++11
  target_compile_features(${example} PRIVATE cxx_std_11)

  # directories to include
  target_include_directories(${example} PRIVATE ${SUNDIALS_INC_DIR})

  # libraries to link against
  target_link_libraries(${example} ${SUNDIALS_LIBS})

  # add the example to ctest
  add_test(NAME ${example} COMMAND ${example})

endforeach()
